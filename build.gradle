buildscript {
repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

//add dependency repositories
repositories {
	maven {
		name = "CB Repo"
		url = "http://chickenbones.net/maven"
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

group = "codechicken" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Translocators"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
    compile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:deobf"
    compile "codechicken:CodeChickenCore:${config.mc_version}-${config.ccc_version}:deobf"
    //compile "codechicken:NotEnoughItems:${config.mc_version}-${config.nei_version}:deobf"
}

version =  "${project.config.mod_version}." + System.getenv("BUILD_NUMBER") ?: "1"

println  config.mc_version + "-" + config.forge_version
// Setup the forge minecraft plugin data. Specify the preferred forge/minecraft version here
minecraft {
    version = config.mc_version + "-" + config.forge_version
    mappings = config.mappings
	makeObfSourceJar = false
	runDir = "run"
}

processResources {
    //redo task if any of these properties change
    inputs.property "version", project.version
    inputs.property "mc_version", config.mc_version

    // Replace properties in info files
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
        expand 'version':project.version,'mc_version':config.mc_version
    }
    // Copy everything else
    from(sourceSets.main.resources.srcDirs) {
        include 'assets/**/*.*'
    }
}

version = "${project.minecraft.version}-${project.version}"

jar {
    classifier = 'universal'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Tell the artifact system about our extra jars
artifacts {
    archives srcJar, deobfJar
}

// Configure an upload task.
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven")
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'Translocators'
                    url 'https://github.com/covers1624/Translocators'
                    scm {
                        url 'https://github.com/covers1624/Translocators'
                        connection 'scm:git:git@github.com:covers1624/Translocators.git'
                        developerConnection 'scm:git:git@github.com:covers1624/Translocators.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/covers1624/Translocators/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://raw.githubusercontent.com/covers1624/Translocators/master/LICENSE.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
						developer {
                            id 'chicken-bones'
                            name 'chicken-bones'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}